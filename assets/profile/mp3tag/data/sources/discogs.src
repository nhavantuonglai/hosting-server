# ###################################################################
# Mp3tag Tag Source for Discogs API (Official)
#
# Search by ALBUM
# 
# This file should be stored in your tag sources sources directory
# %APPDATA%\Mp3tag\data\sources
# and requires Mp3tag v2.64 or above.
#
# [2014-10-26] v1.02
# [2014-10-26]  CHG: changed to use %20 as word separator
# [2014-10-03] v1.01
# [2014-10-03]  CHG: added image caching through proxy 
# [2014-09-28]  FIX: join was not interpreted correctly for artists
# [2014-09-20] v1.00
#
# ###################################################################

[Name]=Discogs.com
[BasedOn]=www.discogs.com
[IndexUrl]=http://api.discogs.com/database/search?release_title=%s
[AlbumUrl]=
[WordSeparator]=%20
[IndexFormat]=%Artist%|%Album%|%_url%|%Format%|%Year%|%Label%|%Country%|%_preview%
[SearchBy]=%album%
[UserAgent]=1
[Encoding]=url-utf-8

[ParserScriptIndex]=...
# ###################################################################
#					I  N  D  E  X
# ###################################################################
json "ON"

json_foreach "results"

	json_select "type"

	if "release"	
	
		# Artist
		json_select "title"
		sayregexp ".+?(?= -.*)"
		say "|" 

		# Album
		sayregexp "(?<= - ).*"
		say "|"

		# URL
		json_select "resource_url"
		sayrest
		say "|"

		# Format
		json_select_array "format" 1
		sayrest
		say "|"

		# Year
		json_select "year"
		sayrest
		say "|"

		# Label
		json_select_array "label" 1
		sayrest
		say "|"

		# Country
		json_select "country"
		sayrest
		say "|"

		# Preview
		say "http://www.discogs.com/release/"
		json_select "id"
		sayrest

		saynewline

	endif

json_foreach_end


[ParserScriptAlbum]=...
# ###################################################################
#					A  L  B  U  M
# ###################################################################
json "ON"

# Album
outputto "ALBUM"
json_select "title"
sayrest

# Discogs_Release_ID
outputto "DISCOGS_RELEASE_ID"
json_select "id"
sayrest

# Coverurl
outputto "coverurl"
json_foreach "images"
	json_select "type"
	if "primary"
		json_select "uri"
		say "http://cache.mp3tag.de/coxy"
		sayregexp "(?<=\.com).*"
		#sayrest
	endif
json_foreach_end

# Catalog Number
outputto "CATALOGID"
json_select_many "labels" "catno" ", "
sayrest

# Publisher
outputto "PUBLISHER"
json_select_many "labels" "name" ", "
sayrest

# Media Type
outputto "MEDIATYPE"
json_select_many "formats" "name" ", "
sayrest
json_select_many "formats" "descriptions" ", "
ifnot ""
	say " "
endif
sayrest

# Genre
outputto "GENRE"
json_select_array "genres" -1 ", "
sayrest

# Style
outputto "STYLE"
json_select_array "styles" -1 ", "
sayrest

# Country
outputto "COUNTRY"
json_select "country"
sayrest

# Year
outputto "YEAR"
json_select "year"
sayrest

# Www
outputto "WWW"
json_select "uri"
sayrest

# Artist or Albumartist
json_foreach "tracklist"
	json_select_many "artists" "name" ", "
json_foreach_end

ifnot ""
	## Various Artists
	outputto "ALBUMARTIST"
else
	## Single Artist
	outputto "ARTIST"
endif

json_foreach "artists"
	json_select "anv"
	ifnot ""
		sayrest
	else
		json_select "name"
		sayrest
	endif

	json_select "join"
	ifnot ""
		say " "
		sayrest
		say " "
	endif
json_foreach_end

# Tracks
json_foreach "tracklist"
	json_select "type_"
	if "track"
		outputto "TRACKS"
		json_select "title"
		sayrest
		say "|"

		outputto "_LENGTH"
		json_select "duration"
		sayrest
		say "|"

		json_select_many "artists" "name" ", "
		ifnot ""
			outputto "ARTIST"
			set "tmp_join"
			json_foreach "artists"
				sayoutput "tmp_join"
				set "tmp_join"
				json_select "anv"
				ifnot ""
					sayrest
				else
					json_select "name"
					sayrest
				endif

				outputto "tmp_join"
				json_select "join"
				ifnot ""
					say " "
					sayrest
					say " "
				endif
				outputto "ARTIST"
			json_foreach_end
			say "|"
		endif
	endif
json_foreach_end

set "tmp_join"